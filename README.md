# ЯндексMQ SDK для 1С:Предприятия

Библиотека для работы с [очередями сообщений Яндекс Message Queue](https://yandex.cloud/ru/services/message-queue) из 1С:Предприятия. 
Позволяет разработчикам на платформе 1С:Предприятие создавать программы, взаимодействующие с облачным брокером сообщений ЯндексMQ (YMQ).

## Быстрый старт

```bsl
// для работы с ЯндексMQ обязательно требуются AccessKey и SecretKey.
// Это своеобразные логин и пароль для доступа к сервису.
// как их получить описано на https://yandex.cloud/ru/docs/iam/operations/sa/create-access-key

// создаем клиент для обращения к сервису
ПараметрыКлиента = Новый Структура;
ПараметрыКлиента.Вставить( "AccessKey", "YC2Gyh6..." );
ПараметрыКлиента.Вставить( "SecretKey", "YC2Jnb2..." );
ЯндексMQ  = Обработки.ЯндексMQ2.НовыйКлиент( ПараметрыКлиента );

// создадим новую очередь
Запрос = Новый Структура;
Запрос.Вставить( "QueueName", "my-tasks" );
UrlОчереди = ЯндексMQ.СоздатьОчередь( Запрос );

// отправим сообщение в очередь
Запрос = Новый Структура;
Запрос.Вставить( "QueueUrl", UrlОчереди );
Запрос.Вставить( "MessageBody", "Hello, world!" );
ЯндексMQ.ОтправитьСообщение( Запрос );

// получим сообщения из очереди
Запрос = Новый Структура;
Запрос.Вставить( "QueueUrl", UrlОчереди );
Сообщения = ЯндексMQ.ПолучитьСообщение( Запрос );

// обработаем все полученные сообщения
Для Каждого ДанныеСообщения Из Сообщения Цикл
    // КакТоОбработать( ДанныеСообщения.Body );
    // успешно обработанные - удалим из очереди
    Запрос = Новый Структура;
    Запрос.Вставить( "QueueUrl", UrlОчереди );
    Запрос.Вставить( "ReceiptHandle", ДанныеСообщения.ReceiptHandle );
    ЯндексMQ.УдалитьСообщение( Запрос );
КонецЦикла;
```
## Как установить

Скачайте файл конфигурации (.cf) [в разделе "Releases"](https://github.com/leemuar/yandexmq-sdk-1c/releases) и перенесите из него обработку "ЯндексMQ" в свою конфигурацию.
Рекомендуется переносить обработку напрямую из файла конфигурации, не сохранять ее как внешнюю: обработка конфигурации содержит модуль менеджера с удобным методом-фабрикой, при сохранении обработки как внешней  модуль менеджера теряется

## Как использовать

Библиотека использует объектный подход к своей работе. Для начала работы необходимо создать объект клиента для обращения к сервису. В модуле менеджера обработки есть готовый метод-фабрика для этого:

```bsl
// для работы с ЯндексMQ обязательно требуются AccessKey и SecretKey.
// Это своеобразные логин и пароль для доступа к сервису.
// как их получить описано на https://yandex.cloud/ru/docs/iam/operations/sa/create-access-key
ПараметрыКлиента = Новый Структура;
ПараметрыКлиента.Вставить( "AccessKey", "YC2Gyh6..." );
ПараметрыКлиента.Вставить( "SecretKey", "YC2Jnb2..." );
ЯндексMQ  = Обработки.ЯндексMQ2.НовыйКлиент( ПараметрыКлиента );
```

После создания объекта клиента можно вызывать его методы, выполняющие запросы к сервису:

```bsl
// создадим новую очередь
Запрос = Новый Структура;
Запрос.Вставить( "QueueName", "my-tasks" );
UrlОчереди = ЯндексMQ.СоздатьОчередь( Запрос );

// получим сообщения из очереди
Запрос = Новый Структура;
Запрос.Вставить( "QueueUrl", UrlОчереди );
Сообщения = ЯндексMQ.ПолучитьСообщение( Запрос );
```

Библиотека предлагает 2 набора методов: простые и детальные. 

Простые стараются сделать максимум за вас: выполнить запрос и вернуть результат в удобном виде. Они возвращают **результат** операции: успешно или нет, данные сообщения или неопределено и т.п. Например, `УдалитьОчередь()` вернет `Истина` в случае успешного удаления, и `Ложь` - в случае ошибок. Или `СоздатьОчередь()` возвращает строку URL созданной очереди в случае успеха или `Неопределено` - при ошибке. Если ошибка при запросе все же произойдет - простые методы не дадут детальной информации почему. Здесь могут пригодиться детальные методы.

Детальные методы заканчиваются на `-Response` или `-Ответ`: `CreateQueueResponse()`, `ОчиститьОчередьОтвет()` и пр. Эти методы возвращают детальные **данные ответа**: код http, тело ответа, заголовки и т.п. Их можно разобрать, залогировать или написать более подробную обработку ответа самостоятельно:

```bsl
Ответ = ЯндексMQ.УдалитьОчередьОтвет( ПараметрыЗапроса );
Если 200 <> Ответ.КодСостояния Тогда
    ЛогОшибка( Ответ.Заголовки, Ответ.Тело, Ответ.КодСостояния );
КонецЕсли;
```

Тело сообщения в ответе хранится в виде двоичных данных. Можно использовать вспомогательные функции библиотеки для преобразования двоичных данных во что-то другое, например json в структуру:

```bsl
Ответ  = ЯндексMQ.ПолучитьАтрибутыОчередиОтвет( ПараметрыЗапроса );
Данные = ЯндексMQ.КакJson( Ответ );
ТекстТелаОтвета = ЯндексMQ.КакТекст( Ответ );
```

## Исключения

Методы библиотеки не перехватывают исключения. Считается, что библиотека не должна решать внутри себя такие ситуации как таймауты, отсутствие связи, некорректный адрес и др. Все это внештатные, исключительные ситуации, на которые должно быть брошено исключение в вызывающий код, а не возвращено какое-то значение. Всегда оборачивайте вызов методов в Попытку.
